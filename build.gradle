buildscript {
	ext {
		springBootVersion = '1.4.0.RELEASE'
	}
	repositories {
		mavenLocal()
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath('se.transmode.gradle:gradle-docker:1.2')
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.13'
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'eclipse'
}

subprojects {
	apply plugin: 'application'
	apply plugin: 'spring-boot'
	apply plugin: 'docker'

	version =  '0.1.0'
	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'

	applicationDefaultJvmArgs = ['-Xmx1g', '-Xms512m']

	repositories {
		mavenLocal()
		mavenCentral()
	}

	docker {
		baseImage "java:8"
		maintainer 'utsman <zeehastory@gmail.com>'
	}

	task buildDocker(type: Docker, dependsOn: build) {
		applicationName = "${jar.baseName}"
		push = project.hasProperty('push')
		tag = "utsman/${project.rootProject.name}-${applicationName}"
//		tagVersion = "latest"

		volume "/tmp"
		addFile("${libsDir}/${project.name}-${version}.jar", "app.jar")
		runCommand("sh -c 'touch /app.jar'")
		entryPoint(["sh", "-c", "java -Djava.security.egd=file:/dev/./urandom -jar /app.jar"])
	}

	dependencyManagement {
		imports {
			mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Camden.SR4'
		}
	}
	dependencies {
		compile 'org.springframework.cloud:spring-cloud-starter-config'
		testCompile 'org.springframework.boot:spring-boot-starter-test'
	}
}

project(':config-server') {
	dependencies {
		compile 'org.springframework.cloud:spring-cloud-config-server'
		compile 'org.springframework.cloud:spring-cloud-starter-eureka'
	}
}

project(':discovery-server') {
	dependencies {
		compile 'org.springframework.cloud:spring-cloud-starter-eureka-server'
	}
}


